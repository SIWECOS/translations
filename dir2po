#!/usr/bin/perl
# Script to convert a directory structur of translations
# into a php file for the siwecos application
use strict;
use warnings;
use FindBin qw($Bin $Script);
use POSIX;
use Text::Wrap;
use utf8;
use Getopt::Long qw(:config no_ignore_case);

my $EXT='.wiki';

binmode(STDOUT, ":utf8");

my ( # options
    $silent,
);
die "Unknown option." unless GetOptions(
    's'          => \$silent,
);

my $LEAD_LANGUAGE= "de";
$Text::Wrap::columns= 75;

# Location and names of some directories / files
# $Bin = absolute path of this script
# $Script = filename of this script
my $TRANSLATIONS= "$Bin/translations";
my $WIKITEXT= "$Bin/wiki";
my $LANGUAGE_DIR= "$Bin/poedit";
my $LANGUAGE_FILE="siwecos";
my $TEMPLATEFILE= "$Bin/$Script.txt";

# Expected purposes
my $BACKGROUND   = 'Background';
my $CATEGORY     = 'Category';
my $COMMENT      = 'Comment';
my $CONSEQUENCE  = 'Consequence';
my $DESCRIPTION  = 'Description';
my $DETAILS      = 'Details';
my $HEADLINE     = 'Headline';
my $IMPACT       = 'Impact';
my $LINK         = 'Link';
my $ERROR_TEXT   = 'Negative';
my $SUCCESS_TEXT = 'Positive';
my $TIPP         = 'Solution_Tips';
my $TECHNICAL    = 'Technical';

my %IGNORE= (
    Template => 1,
);

# Prepare some output stuff
my @SEQUENCE; # Sequence of placeholders in output
my %SUFFIX;   # Suffix for the placholder
my $v=0;
foreach (
    # This defines the sequence in the output file
    # As well as the SUFFIX appended to the placholder
    $HEADLINE     => '',
    $CATEGORY     => 'CATEGORY', # not yet used but will be used in Wiki
    $SUCCESS_TEXT => 'SUCCESS',
    $ERROR_TEXT   => 'ERROR',
    $DESCRIPTION  => 'ERROR_DESC',
    $TIPP         => 'TIPS',
    $LINK         => 'LINK',
    # above suffixes are required by the application
    # below suffixes are not used yet
    $BACKGROUND   => 'Background',
    $COMMENT      => 'Comment',
    $CONSEQUENCE  => 'Consequence',
    $DETAILS      => 'Details',
    $IMPACT       => 'Impact',
    $TECHNICAL    => 'Technical',
) {
    # This will set up the required data structures
    if ($v) {
        my $suffix= $_;
        $suffix= "_$_" if $_;
        $SUFFIX{$SEQUENCE[$#SEQUENCE]}= $suffix;
    } else {
        push @SEQUENCE, $_;
    }
    $v= 1-$v;
}

# First prepare the template
open my $template, '<', $TEMPLATEFILE
    or die "Cannot read template $TEMPLATEFILE: $!\n";
my @template= <$template>;
close $template;

    die "Can't access directory $TRANSLATIONS\n"
unless -d $TRANSLATIONS
   and -r $TRANSLATIONS
   and -x $TRANSLATIONS;
# get all translation filenames
# Directory structure is $TRANSLATIONS / placeholder / purpose / language $EXT
# Also collect categories
my $language_data= {};
my $categories= {};
my %category_missing;
my %category_found;
my %translations_found;
my %translations_language;
my %all_placeholders;
while( glob("$TRANSLATIONS/*/*/*$EXT") ) {
    # Split pathname into parts
    my ($placeholder, $purpose, $lang)= (split(m#/#, $_))[-3..-1];
    # remove filename extension
    $lang=~ s#\.[^./]+$##;
    # Read the file
    if( open my $trans, '<:encoding(utf8)', $_ ) {
        # into our data structures
        my $text= do {
            local $/;
            <$trans>;
        };
        # If a category is set, setup the category data structure
        if( $purpose eq "Category" ) {
            chomp($text);
            $language_data->{$lang}->{$placeholder} ||= {};
            $categories->{$lang}->{$text}->{$placeholder}= $language_data->{$lang}->{$placeholder};
            $language_data->{$lang}->{$placeholder}->{$purpose}= $text;
            ++$category_found{$lang}->{$placeholder};
        } elsif( defined $SUFFIX{$purpose} ) {
            $language_data->{$lang}->{$placeholder}->{$purpose}= $text;
            ++$category_missing{$lang}->{$placeholder};
            my $text= "$purpose for $placeholder";
            ++$translations_found{$text};
            ++$translations_language{$lang}->{$text};
            $all_placeholders{$placeholder}->{$purpose}= "$placeholder" . $SUFFIX{$purpose};
        } elsif( not defined $IGNORE{$purpose} ) {
            warn "Unknown Purpose: $purpose for $placeholder\n";
        }
    }
    else {
        warn "Can't read $_: $!";
    }
}
# All application related translations are in memory now
while( glob("$WIKITEXT/*/*/*$EXT") ) {
    # Split pathname into parts
    my ($placeholder, $purpose, $lang)= (split(m#/#, $_))[-3..-1];
    # remove filename extension
    $lang=~ s#\.[^./]+$##;
    # Read the file
    if( open my $trans, '<:encoding(utf8)', $_ ) {
        # into our data structures
        my $text= do {
            local $/;
            <$trans>;
        };
        # If a category is set, setup the category data structure
        $language_data->{$lang}->{$placeholder}->{$purpose}= $text;
        $text= "$purpose for $placeholder";
        ++$translations_found{$text};
        ++$translations_language{$lang}->{$text};
        $all_placeholders{$placeholder}->{$purpose}= "$placeholder::$purpose";
    }
    else {
        warn "Can't read $_: $!";
    }
}

# create a backup of existing language files
if ( -e $LANGUAGE_DIR ) {
    my $newname= $LANGUAGE_DIR . "_" . strftime("%Y-%m-%d_%H-%M-%S", localtime time);
    rename $LANGUAGE_DIR, $newname
        or die "Failed to create backup $newname: $!\n";
}
# Create a new language file directory
mkdir "$LANGUAGE_DIR" or die "Failed to create directory $LANGUAGE_DIR: $!\n";

# Go through each language
my $lead= $language_data->{$LEAD_LANGUAGE};
foreach my $language (keys %$categories) {
    # Check which categories in this language we are missing
    delete @{$category_missing{$language}}{ keys %{$category_found{$language}} };
    foreach my $placeholder (sort keys %{$category_missing{$language}}) {
        warn "Missing $language category for $placeholder\n";
        $categories->{$language}->{"* NO CATEGORY *"}->{$placeholder}=$language_data->{$language}->{$placeholder};
    }
    # Check which translations we are missing
    if (not $silent) {
        foreach (sort keys %translations_found) {
            next if exists $translations_language{$language}->{$_};
            warn "Missing $language $_\n";
        }
    }
    my $cats= $categories->{$language};
    # Create the language file for the current language
    my $language_file= "$LANGUAGE_DIR/$LANGUAGE_FILE";
    my $ist_template= 0;
    # The lead language will become the po template
    if( $language eq $LEAD_LANGUAGE ){
        $language_file.= '.pot';
        $ist_template= 1;
    } else {
        $language_file.=  "_$language.po";
    }
    open my $out, '>:encoding(utf8)', $language_file
        or die "Failed to create $language_file: $!\n";
    # Work through the text-template lines
    foreach (@template) {
        # Until the placeholder /* DATA HERE */ is found
        if( m#^\s*/\*\s*DATA\s*HERE\s*\*/\s*#)  {
            # Go through each placeholder in alphabetical order
            my $placeholders= $language_data->{$language};
            foreach my $placeholder (sort keys %$lead) {
                my $p= $placeholders->{$placeholder};
                my $category= ($p && $p->{$CATEGORY}) || '*missing*';
                # Go through each of the purposes we want to print
                # They are defined at the top under "# Prepare some output stuff"
                foreach my $purpose (@SEQUENCE) {
                    my $text= ($p && $p->{$purpose}) || '';
                    my $suffix= $SUFFIX{$purpose};
                    my $ph= "$placeholder$suffix";
                    my $lead_text= $lead->{$placeholder}->{$purpose};
                    if( $lead_text ) {
                        # Print the placeholder as a comment
                        # Print the category as comment
                        print $out "\n# Category: $category\n";
                        print $out "# Placeholder: $placeholder\n";
                        print $out "# Purpose: $purpose\n";
                        print $out qq'msgctxt "$ph"\n';
                        print $out msg(msgid => $lead_text);
                        if( $ist_template ) {
                            print $out msg(msgstr => "");
                        } else {
                            print $out msg(msgstr => $text);
                        }
                        print $out "\n";
                    } else {
                        #print $out "# No text for $ph\n\n";
                    }
                }
                foreach my $purpose (qw( link wikitext )) {
                    my $text= $p->{$purpose};
                    my $ph= "$placeholder::$purpose";
                    if( $text ) {
                        # Print the placeholder as a comment
                        # Print the category as comment
                        print $out "\n# Placeholder: $placeholder\n";
                        print $out "# Purpose: $purpose\n";
                        print $out qq'msgctxt "$ph"\n';
                        print $out msg(msgid => $lead->{$placeholder}->{$purpose});
                        if( $ist_template ) {
                            print $out msg(msgstr => "");
                        } else {
                            print $out msg(msgstr => $text);
                        }
                        print $out "\n";
                    } else {
                        #print $out "# No text for $ph\n\n";
                    }
                }
            }
        } else {
            my $line= $_;
            for ($line) {
                s#/\*\s*LANGUAGE\s*\*/#$language#g;
                s#/\*\s*now\s+(.*?)\s*?\*/#strftime $1, localtime time#ge;
            }
            # Print an unmodified template line
    		print $out $line;
		}
	}
}

# trim leading and trealing whitespace
sub trim {
    local($_)= @_;
    s/^\s+|\s+$//gs;
    return $_;
}

sub msg {
    my($id, $text)= @_;
    local($_)= quoted_form_of($text);
    return qq'$id $_\n';
}

sub quoted_form_of {
    local($_)= @_;
    # replace \ and " with \\ or \"
    s/(["\\])/\\$1/sg;
    # do some splitting and joining. Read from inside to outside
    $_= join qq'\\n"\n"', map {
        s/\s+$//g; # trim each line
        if( length($_) > $Text::Wrap::columns ) { # If it's too long
            my $x= $_;
            $_= eval {
                local($Text::Wrap::huge) = 'overflow';
                wrap('','',$_);# then wrap it
            };
            {
                no warnings 'uninitialized';
                die "$@\n\n$x\n " if $@;
            }
            s/\n/ "\n"/gs; # Put quotes in where the line was wrapped
        }
        $_; # return the result
    } split /\n/; # Split the line at linefeeds
    my $splitted= /\n/ ? qq'""\n' : '';
    return qq'$splitted"$_"';
}
